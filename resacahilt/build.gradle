plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'dagger.hilt.android.plugin'
    id 'maven-publish'
    id 'kotlin-kapt'
}

group = 'com.github.sebaslogen'

android {
    compileSdk versions.compileSdk

    defaultConfig {
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        buildToolsVersion versions.buildTools
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures { // Enables Jetpack Compose for this module
        compose = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion versions.composeCompiler
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    // Config publication component to publish artifacts to JitPack
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {

    api project(":resaca")

    implementation "androidx.core:core-ktx:$versions.coreKtx"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.kotlinxCoroutines"

    // Compose dependencies
    implementation "androidx.compose.compiler:compiler:$versions.composeCompiler"
    // Integration with ViewModels
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$versions.lifecycleViewModel"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycleViewModel"

    // Hilt dependencies
    implementation "androidx.hilt:hilt-navigation-compose:$versions.hiltNavigationCompose"
    implementation "com.google.dagger:hilt-android:$versions.hilt"
    kapt "com.google.dagger:hilt-compiler:$versions.hilt"
}

// Config for publishing artifacts to JitPack
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                artifactId = 'resacahilt'
            }
        }
    }
}